/*
 * Copyright (C) 2016-2020  Irotsoma, LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>
 */
buildscript {
    ext.commonVersion='0.4-SNAPSHOT'
    ext.kotlin_version = '1.3.30'
    ext.dokkaVersion = '0.9.17'
    ext.junitVersion = '4.12'
    ext.bouncyCastleVersion = '1.61'
    ext.kotlinLoggingVersion = '1.6.22'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}
plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group 'com.irotsoma.cloudbackenc.encryption'
version '0.4-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
    //if the current project is a snapshot then allow using snapshot versions from maven central  Having this as an if prevents accidentally building a release version with snapshot dependencies.
    if (project.version.endsWith('-SNAPSHOT')){
        maven{
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.irotsoma.cloudbackenc:common:$commonVersion"
    compile "com.irotsoma.cloudbackenc.common:encryption:$commonVersion"
    //include bouncy castle as a runtime requirement and shadow it so the class path will include the library
    runtime "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"
    shadow "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"
    //logging
    compile "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
    //test
    testCompile "junit:junit:$junitVersion"
}

apply plugin: 'signing'
def isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    sign configurations.archives
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
//copy the bouncy castle jar to the output directory. It must accompany this extension separately since it is signed.
task copyBCJar(type: Copy){
    dependsOn assemble
    from configurations.runtime{
        include 'bcprov*.jar'
    }
    into "$buildDir/libs/"
}
build.dependsOn 'copyBCJar'
if (!isReleaseVersion) {
    configurations.all {
        // check for updates every build
        resolutionStrategy{
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

sourceSets {
    main {
        java{
            srcDir 'src/main/kotlin'
        }
        resources {
            srcDir "$buildDir/license-reports/"
        }
    }
}
compileKotlin {
    kotlinOptions.jvmTarget= "1.8"
}
//javadoc stuff for Kotlin
apply plugin: 'org.jetbrains.dokka'
dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs/javadoc"
}
task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}
//build a source jar
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//configure shadowJar to only include this extension's classes
//shadowJar will additionally create the appropriate class path entries for the bouncy castle library
//JCE encryption providers must now be signed so they can't be included in the extension jar
shadowJar{
    dependencies{
        include (dependency('com.irotsoma.cloudbackenc.encryption:cloudbackenc-encryption-boundcycastle'))
    }
}
artifacts {
    archives shadowJar
    archives sourceJar
    archives javadocJar
}
//this section downloads some reports regarding the licenses of various dependencies and includes them in the
// META-INF/licenses folder
apply plugin: 'com.github.hierynomus.license'
license {
    ignoreFailures true
    mapping ('kt', 'JAVADOC_STYLE')
    excludes(['**/*.json', '**/*.properties', '**/LICENSE', '**/*license*.html', '**/*license*.xml', '**/COPYING', '**/COPYING.LESSER', '**/*.dat'])
}
task copyLicenseReports(type: Copy){
    from "$buildDir/reports/license/"
    into "$buildDir/license-reports/META-INF/licenses"
}
assemble.dependsOn 'downloadLicenses'
assemble.dependsOn 'copyLicenseReports'
copyLicenseReports.mustRunAfter 'downloadLicenses'


